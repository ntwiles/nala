use std::str::FromStr;

use crate::ast;

grammar;

match {
    "{", "}", "(", ")", "[", "]", ";", ":", ".", ",", "::", "=>",
    "const", "mut", "if", "elif", "else", "for", "in", "wiles", "func", 
    "break", "struct", "enum", "match",
    "=", "==", ">", "<", "+", "-", "*", "/",
    "Array", "Bool", "Func", "Number",  "Object", "String", "Void",

    r"[a-zA-Z_\-]+",        // symbols
    r"-?[0-9]+(\.[0-9]+)?", // number literals
    r"'[^']*'",             // string literals

    // Skip whitespace and comments
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // `// comment`
    r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { }, // `/* comment */`
}

pub Program: ast::Program = {
    <Block> => ast::Program::Block(<>),
    <Stmts> => ast::Program::Stmts(<>)
}

Block: ast::Stmts = {
    "{" <Stmts> "}" => <>
}

pub Stmts: ast::Stmts = {
    <ss:Stmts> <s:Stmt> => ast::Stmts::Stmts(Box::new(ss), s),
    Stmt => ast::Stmts::Stmt(<>),
};

Stmt: ast::Stmt = {
    "const" <s:Symbol> <t:TypeDeclaration?> "=" <e:Expr> ";" => ast::Stmt::Declare(s, e, t, false),
    "mut" <s:Symbol> <t:TypeDeclaration?> "="  <e:Expr> ";" => ast::Stmt::Declare(s, e, t, true),
    <c:IfElseChain> => ast::Stmt::IfElseChain(Box::new(c)),
    "for" <s:Symbol> "in" <e:Expr> <b:Block> => ast::Stmt::For(s,e, Box::new(b)),
    "wiles" "(" <e:Expr> ")" <b:Block> => ast::Stmt::Wiles(e, Box::new(b)),
    "func" <s:Symbol> "(" <p:Params> ")" ":" <r:TypeVariant> <b:Block> => 
        ast::Stmt::Func(ast::funcs::Func{ ident: s, params: p, return_type: r, block: Box::new(ast::FuncVariant::Nala(b))}),
    "break" "(" <Expr> ")" ";" => ast::Stmt::Break(<>),
    "struct" <s:Symbol> <t:TypeArgs?> "{" <ff:StructFields> "}" => ast::Stmt::Struct(s, t, ff),
    "enum" <s:Symbol> <t:TypeArgs?> "{" <v:VariantsDeclare> "}" => ast::Stmt::Enum(s, t, v),
    <s:PlaceExpression> "=" <e:Expr> ";" => ast::Stmt::Assign(s, e),
    <Expr> ";" => ast::Stmt::Expr(<>),
    <Match> => ast::Stmt::Match(<>)
};

TypeDeclaration: ast::types::type_literal_variant::TypeLiteralVariant = {
    ":" <TypeVariant> => <>
};

TypeArgs: ast::types::TypeArgs = {
    "<" <s:Symbol> ">" => ast::types::TypeArgs::Generic(s)
};

IfElseChain: ast::branching::IfElseChain = { 
    "if" "(" <c:Expr> ")" <b:Block> <ei:ElseIf*> <e:Else?> => 
        ast::branching::IfElseChain { cond: c, block: b, else_ifs: ei, else_block: e },
};

Match: ast::branching::Match = {
    "match" "(" <e:Expr> ")" "{" <c:MatchCase*> "}" => ast::branching::Match { expr: e, cases: c }
};

MatchCase: ast::branching::MatchCase = {
    <p:Pattern> "=>" <b:Block> => ast::branching::MatchCase { pattern: p, block: b }
};

Pattern: ast::patterns::Pattern = {
    <e:Symbol> "::" <v:Symbol> "(" <i:Symbol> ")" => ast::patterns::Pattern::Variant(e, v, Some(i)),
    <e:Symbol> "::" <v:Symbol> => ast::patterns::Pattern::Variant(e, v, None),
};

ElseIf: ast::branching::ElseIf = {
    "elif" "(" <e:Expr> ")" <b:Block> => ast::branching::ElseIf{ cond: e, block: b }
};

Else: ast::branching::Else = {
    "else" <Block> => ast::branching::Else{ block: <> }
};

PlaceExpression: ast::PlaceExpression = {
    <p:PlaceExpression> "[" <e:Expr> "]" => ast::PlaceExpression::Index(Box::new(p), Box::new(e)),
    <MemberAccess> => ast::PlaceExpression::MemberAccess(Box::new(<>)),
    <Symbol> => ast::PlaceExpression::Symbol(<>),
};

VariantsDeclare = Comma<VariantDeclare>;

VariantDeclare: ast::types::variant_declare::VariantDeclare = {
    <Symbol> => ast::types::variant_declare::VariantDeclare::Empty(<>),
    <s:Symbol> "(" <t:TypeVariant> ")" => ast::types::variant_declare::VariantDeclare::Data(s, t)
};

Elems = Comma<Expr>;

Expr: ast::Expr = {
    <l:Expr> "==" <r:EnumVariantOrAddend> => ast::Expr::Eq(Box::new(l), r),
    <l:Expr> ">" <r:Addend> => ast::Expr::Gt(Box::new(l), r),
    <l:Expr> "<" <r:Addend> => ast::Expr::Lt(Box::new(l), r),
    EnumVariantOrAddend => ast::Expr::EnumVariant(<>),
    Array => ast::Expr::Array(<>),
    Object => ast::Expr::Object(<>),
};

KeyValuePairs = Comma<KeyValuePair>;

KeyValuePair: ast::objects::KeyValuePair = {
    <s:Symbol> ":" <e:Expr> => ast::objects::KeyValuePair{ key: s, value: Box::new(e)}
}

Params = Comma<Param>;

Param: ast::funcs::Param = {
    <s:Symbol> ":" <t:TypeVariant> => ast::funcs::Param { ident: s, param_type: t }
}

Object: ast::objects::Object = {
    "{" <KeyValuePairs> "}" => ast::objects::Object { entries: <>}
}

Array: ast::arrays::Array = {
    "[" <Elems> "]" => ast::arrays::Array { elems: <> },
};

StructFields = Comma<StructField>;

StructField: ast::types::StructLiteralField = {
    <s:Symbol> ":" <t:TypeVariant> => ast::types::StructLiteralField { ident: s, value: ast::types::StructLiteralFieldValue::Type(t) },
    <s:Symbol> ":" "{" <ff:StructFields> "}" => ast::types::StructLiteralField { ident: s, value: ast::types::StructLiteralFieldValue::Nested(ff) }
}

TypeVariants = Comma<TypeVariant>;

TypeVariant: ast::types::type_literal_variant::TypeLiteralVariant = {
    <t:Type> "<" <tt:TypeVariants> ">" => ast::types::type_literal_variant::TypeLiteralVariant::Composite(t, tt),
    <Type> => ast::types::type_literal_variant::TypeLiteralVariant::Type(<>),
}

Type: ast::types::type_literal::TypeLiteral = {
    <PrimitiveType> => ast::types::type_literal::TypeLiteral::PrimitiveType(<>),
    <Symbol> => ast::types::type_literal::TypeLiteral::UserDefined(<>),
}

PrimitiveType: ast::types::primitive_type::PrimitiveType = {
    "Array" => ast::types::primitive_type::PrimitiveType::Array,
    "Bool" => ast::types::primitive_type::PrimitiveType::Bool,
    "Func" => ast::types::primitive_type::PrimitiveType::Func,
    "Number" => ast::types::primitive_type::PrimitiveType::Number,
    "String" => ast::types::primitive_type::PrimitiveType::String,
    "Void" => ast::types::primitive_type::PrimitiveType::Void,
}

EnumVariantOrAddend: ast::types::enum_variant::EnumVariantOrAddend = {
    <s:Symbol> "::" <ss:Symbol> "(" <e:Expr> ")" => ast::types::enum_variant::EnumVariantOrAddend::EnumVariant(s, ss, Some(Box::new(e))),
    <s:Symbol> "::" <ss:Symbol> => ast::types::enum_variant::EnumVariantOrAddend::EnumVariant(s, ss, None),
    Addend => ast::types::enum_variant::EnumVariantOrAddend::Addend(<>),
};

pub Addend: ast::math::Addend = {
    <l:Addend> "+" <r:Factor> => ast::math::Addend::Add(Box::new(l), r),
    <l:Addend> "-" <r:Factor> => ast::math::Addend::Sub(Box::new(l), r),
    Factor =>  ast::math::Addend::Factor(<>)
};

pub Factor: ast::math::Factor = {
    <l:Factor> "*" <t:Term> => ast::math::Factor::Mult(Box::new(l), t),
    <l:Factor> "/" <t:Term> => ast::math::Factor::Div(Box::new(l), t), 
    Invocation => ast::math::Factor::Invocation(<>),
};

Invocation: ast::funcs::Invocation = {
    <p:PlaceExpression> "(" <e:Elems> ")" => ast::funcs::Invocation::Invocation(p, e),
    <PlaceExpression> => ast::funcs::Invocation::PlaceExpression(<>),
    <Value> => ast::funcs::Invocation::Value(<>),
};

Value: ast::terms::Value = {
    Num => ast::terms::Value::Num(<>),
    Str => ast::terms::Value::String(<>),
}

MemberAccess: ast::objects::MemberAccess = {
    <p:MemberAccess> "." <c:Symbol> => ast::objects::MemberAccess::MemberAccesses(Box::new(p), c),
    <p:Symbol> "." <c:Symbol> => ast::objects::MemberAccess::MemberAccess(p, c),
}

Term: ast::terms::Term = {
    Symbol => ast::terms::Term::Identifier(<>),
    Value => ast::terms::Term::Value(<>),
};

Symbol: String = {
    r"[a-zA-Z_\-]+" => String::from_str(<>).unwrap()
};

Num: f32 = {
    r"-?[0-9]+(\.[0-9]+)?" => f32::from_str(<>).unwrap(),
};

Str: String = {
    r"'[^']*'" => String::from_str(&<>[1..<>.len() - 1]).unwrap(),
};

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};